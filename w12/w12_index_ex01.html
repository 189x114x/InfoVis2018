<html>
    <head>
	<title>W12</title>
    </head>
    <body style="margin:0">
	<script src="three.min.js"></script>
	<script src="TrackballControls.js"></script>

	<script type="x-shader/x-vertex" id="Lambertian.vert">
	varying vec3 point_color;
	varying vec4 point_position;
	varying vec3 normal_vector;
	uniform vec3 light_position;

	vec3 LambertianReflection(vec3 C, vec3 L, vec3 N)
	{
		float ka=0.4;
		float kd=0.6;

		float dd=max(dot(N,L),0.0);
		float Ia=ka;
		float Id=kd*dd;
		return C*(Ia+Id);

	}

	void main()
	{
		point_position = modelViewMatrix * vec4( position, 1.0 );
		normal_vector = normalMatrix * normal;

		vec3 C = color;
		vec3 L = normalize( light_position - point_position.xyz );
		vec3 N = normalize( normal_vector );
		point_color = LambertianReflection( C, L, N );
		gl_Position = projectionMatrix * point_position;
	}
	</script>

	<script type="x-shader/x-fragment" id="Lambertian.frag"> varying vec3 point_color;
	void main() {
	gl_FragColor = vec4( point_color, 1.0 ); }
	</script>

	<script type="x-shader/x-vertex" id="Phong.vert">
	varying vec3 point_color;
	varying vec4 point_position;
	varying vec3 normal_vector;
	uniform vec3 light_position;

	vec3 PhongReflection(vec3 C,vec3 L,vec3 N,vec3 V){
		float ka=0.3;
		float kd=0.5;
		float ks=0.8;
		float n=50.0;

		vec3 R=reflect(-L,N);
		float dd=max(dot(N,L),0.0);
		float ds=pow(max(dot(R,V),0.0),n);
		if(dd<=0.0){
			ds=0.0;
		}

		float Ia=ka;
		float Id=kd*dd;
		float Is=ks*ds;
		return C*(Ia+Id+Is);
	}

	void main(){
		point_position = modelViewMatrix * vec4( position, 1.0 );
		normal_vector = normalMatrix * normal;

		vec3 C = color;
		vec3 L = normalize( light_position - point_position.xyz );
		vec3 N = normalize( normal_vector );
		vec3 V = normalize( cameraPosition - point_position.xyz);

		point_color = PhongReflection( C, L, N ,V );
		gl_Position = projectionMatrix * point_position;
	}
	</script>

	<script type="x-shader/x-fragment" id="Phong.frag"> varying vec3 point_color;
	void main() {
		gl_FragColor = vec4( point_color, 1.0 );
	}
	</script>

	<script type="x-shader/x-vertex" id="BlinnPhong.vert">
	varying vec3 point_color;
	varying vec4 point_position;
	varying vec3 normal_vector;
	uniform vec3 light_position;

	vec3 BlinnPhongReflection(vec3 C,vec3 L,vec3 N,vec3 V){
		float ka=0.3;
		float kd=0.5;
		float ks=0.8;
		float n=50.0;

		vec3 H=normalize(L+V);
		float dd=max(dot(N,L),0.0);
		float ds=pow(max(dot(N,H),0.0),n);
		if(dd<=0.0){
			ds=0.0;
		}

		float Ia=ka;
		float Id=kd*dd;
		float Is=ks*ds;
		return C*(Ia+Id+Is);
	}

	void main(){
		point_position = modelViewMatrix * vec4( position, 1.0 );
		normal_vector = normalMatrix * normal;

		vec3 C = color;
		vec3 L = normalize( light_position - point_position.xyz );
		vec3 N = normalize( normal_vector );
		vec3 V = normalize( cameraPosition - point_position.xyz);

		point_color = BlinnPhongReflection( C, L, N ,V );
		gl_Position = projectionMatrix * point_position;
	}
	</script>

	<script type="x-shader/x-fragment" id="BlinnPhong.frag"> varying vec3 point_color;
	void main() {
		gl_FragColor = vec4( point_color, 1.0 );
	}
	</script>

	<script type="x-shader/x-vertex" id="CookTorrance.vert">
	varying vec3 point_color;
	varying vec4 point_position;
	varying vec3 normal_vector;
	uniform vec3 light_position;

	vec3 CookTorranceReflection(vec3 C,vec3 L,vec3 N,vec3 V){
		float ka=0.3;
		float kd=0.5;
		float ks=0.8;
		float m=0.1;
		float F0=0.1;

		float dd=max(dot(N,L),0.0);
		float ds=0.0;
		if(dd>0.0){
			vec3 H=normalize(L+V);
			float NL=dd;
			float NH=dot(N,H);
			float NV=dot(N,V);
			float VH=dot(V,H);

			float D=(1.0/(4.0*m*m*NH*NH*NH*NH))*exp((NH*NH-1.0)/(m*m*NH*NH));
			float G=min(min(2.0*NH*NV/VH,2.0*NH*NL/VH),1.0);

			float n=(1.0+sqrt(F0))/(1.0-sqrt(F0));
			float c=dot(L,H);
			float g=sqrt(n*n+c*c-1.0);
			float F=0.5*(((g-c)/(g+c))*((g-c)/(g+c)))*(1.0+((c*(g+c)-1.0)/(c*(g-c)+1.0))*((c*(g+c)-1.0)/(c*(g-c)+1.0)));

			ds=(F/3.14)*(D*G/(NL*NV));
		}

		float Ia=ka;
		float Id=kd*dd;
		float Is=ks*ds;
		return C*(Ia+Id+Is);
	}


	void main(){
		point_position = modelViewMatrix * vec4( position, 1.0 );
		normal_vector = normalMatrix * normal;

		vec3 C = color;
		vec3 L = normalize( light_position - point_position.xyz );
		vec3 N = normalize( normal_vector );
		vec3 V = normalize( cameraPosition - point_position.xyz);

		point_color = CookTorranceReflection( C, L, N ,V );
		gl_Position = projectionMatrix * point_position;
	}
	</script>

	<script type="x-shader/x-fragment" id="CookTorrance.frag"> varying vec3 point_color;
	void main() {
		gl_FragColor = vec4( point_color, 1.0 );
	}
	</script>

	<script type="x-shader/x-vertex" id="Toon.vert">
	varying vec3 point_color;
	varying vec4 point_position;
	varying vec3 normal_vector;
	uniform vec3 light_position;

	vec3 ToonReflection(vec3 C,vec3 L,vec3 N,vec3 V){
		float ka=0.3;
		float kd=0.5;
		float ks=0.8;
		float n=50.0;

		float dd=max(dot(N,L),0.0);
		float a;
		if(dd<0.1)
			a=0.0;
		else if(dd<0.3)
			a=0.3;
		else if(dd<0.6)
			a=0.6;
		else
			a=1.0;

		vec3 R=reflect(-L,N);
		float ds=pow(max(dot(R,V),0.0),n);
		if(dd<=0.0){
			ds=0.0;
		}

		vec3 H=normalize(L+V);
		float b=max(dot(N,H),0.0);
		b=pow(b,n);

		float Ia=ka;
		float Id=a*kd*dd;
		float Is=b*ks*ds;

		return vec3(Ia+Id+Is);
	}

	void main(){
		point_position = modelViewMatrix * vec4( position, 1.0 );
		normal_vector = normalMatrix * normal;

		vec3 C = color;
		vec3 L = normalize( light_position - point_position.xyz );
		vec3 N = normalize( normal_vector );
		vec3 V = normalize( cameraPosition - point_position.xyz);

		point_color = ToonReflection( C, L, N ,V );
		gl_Position = projectionMatrix * point_position;
	}
	</script>

	<script type="x-shader/x-fragment" id="Toon.frag"> varying vec3 point_color;
	void main() {
		gl_FragColor = vec4( point_color, 1.0 );
	}
	</script>

	<script src="https://naohisas.github.io/KVS.js/Build/KVS.min.js"></script>
	<script src="KVS2THREE.js"></script>
	<script src="w12_main_ex01.js"></script>
	<script src="dat.gui.min.js"></script>
	<script>
	 main();




	</script>
    </body>
</html>
